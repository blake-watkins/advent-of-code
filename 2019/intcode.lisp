(eval-when (:compile-toplevel :load-toplevel :execute)
  (ql:quickload "alexandria")
  (use-package :alexandria)
  ;; has tree-map
  (ql:quickload "sycamore")
  (use-package :sycamore)  )



(defclass computer-state ()
  ((program
    :initarg :program
    :initform nil)
   (ip
    :initarg :ip
    :initform 0)
   (input
    :initform nil)
   (input-source
    :initform nil)
   (output
    :initform nil)
   (relative-base
    :initform 0)))

(defun compare-index (a b)
  (cond ((< a b) -1)
	((> a b) 1)
	(t 0)))

(defmethod initialize-instance :after ((state computer-state)
				       &key program-text template)
  (when template
    (loop
       for field in '(program ip input input-source output relative-base)
       do (setf (slot-value state field) (slot-value template field))))

  (when program-text
    (let* ((program-list  (parse-program-text program-text))
	   (program-alist
	    (loop 
	       for index from 0
	       for instruction in program-list
	       collect (cons index instruction)))
	   (program (tree-map-insert-alist
		     (make-tree-map #'compare-index)
		     program-alist)))
	(setf (slot-value state 'program) program))))

(defun parse-program-text (text)
  (run-parser (with-monad
                (assign list (parse-list (parse-number)))
                (parse-newline)
                (unit list))
              text))

;; State a => State -> (a, State)
;; bind :: State -> (a, State) -> (a -> State -> (b, State)) -> State -> (b, State)
;; State Monad Primitives
(defun state-bind (ma function)
  (lambda (state-1)
    (multiple-value-bind (a state-2) (funcall ma state-1)
      (let ((mb (funcall function a)))
	(funcall mb state-2)))))

(defun state-then (ma mb)
  (state-bind ma
	      #'(lambda (x) (declare (ignore x)) mb)))

(defun state-unit (a)
  (lambda (state)
    (values a state)))

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun rewrite-state-forms (forms)
    (flet ((rewrite-state-form (cur rest)
	     (if (eq (car cur) 'assign)
		 (if rest
		     `(state-bind ,(third cur)
				  #'(lambda (,(second cur))
				      ,(rewrite-state-forms rest)))
		     (third cur))
		 (if rest
		     `(state-then ,cur
				  ,(rewrite-state-forms rest))
		     cur))))
      (if (null forms)
	  '()
	  (rewrite-state-form (car forms) (cdr forms))))))
	  
(defmacro with-state-monad (&body body)
  (rewrite-state-forms body))

(defmacro execute-program (computer program)
  (with-gensyms (output new-computer)
    `(multiple-value-bind (,output ,new-computer)
         (funcall ,program ,computer)
       (setf ,computer ,new-computer)
       ,output)))

;; Defines a state primitive reader
;; Defines a function taking state.
;; Evaluates body forms and returns two values: the value of the last body
;;  form and the state.
;; Wraps the body forms in a with-slots for access to the slots of the stat
(defmacro def-state-reader (name args slots &body body)
  (with-gensyms (ret)
    (let ((body `((let ((,ret (progn ,@body)))
                    (values ,ret state)))))
      `(defun ,name ,args
         (lambda (state)
           ,@(if slots
                 `((with-slots ,slots state
                     ,@body))
                 body))))))

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun rewrite-state-writer-body (slots body)
    (labels ((with-slots-code (slots state body)
               (if slots 
                   `((with-slots ,slots ,state
                       ,@body))
                   body))
             (new-slot-entry (slot)
               (let ((new-name
                       (concatenate 'string "NEW-" (symbol-name slot))))
                 (list (intern new-name) slot)))
             (new-state-code (slots body)
               `((let ((new-state (make-instance 'computer-state
                                                 :template state)))
                   ,@(let ((new-slots (mapcar #'new-slot-entry slots)))
                       (with-slots-code new-slots 'new-state body))))))
      
      (with-slots-code slots 'state
        (new-state-code slots body)))))

;; Defines a state primitive writer
;; Defines a function taking state.
;; Wraps the body forms in a with-slots for access to the slots of the state
;; Creates a new state named new-state and wraps the body forms in a
;;  with-slots for access to the slots of the new-state. All the new
;;  slot names are prefixed with "NEW-"
;; Evaluates body forms. If the last form is not a (values ... ) form, then
;;  appends (values nil new-state) to the end of body.
(defmacro def-state-writer (name args slots &body body)
  (let ((final-form (lastcar body)))
    (unless (and final-form
                 (listp final-form)
                 (eq 'values (car final-form)))
      (setf body (append body '((values nil new-state))))))
  `(defun ,name ,args
     (lambda (state)
       ,@(rewrite-state-writer-body slots body))))

;; State primitives

(def-state-reader get-memory (index) (program)
  (tree-map-find program index 0))

(def-state-writer set-memory (index value) (program)
  (setf new-program (tree-map-insert program index value)))

(def-state-reader get-ip () (ip) ip)

(def-state-writer set-ip (value) (ip) (setf new-ip value))

(def-state-writer front-input () (input)
  (setf new-input (cdr input))
  (values (car input) new-state))

(def-state-writer front-output () (output)
  (setf new-output (cdr output))
  (values (car output) new-state))

(def-state-writer push-back-input (value) (input)
  (setf new-input (if (listp value)
		      (append input value)
		      (append input (list value)))))

(def-state-writer push-back-output (value) (output)
  (setf new-output (if (listp value)
		       (append output value)
		       (append output (list value)))))

(def-state-reader queue-length (name) ()
  (length (slot-value state name)))

(def-state-reader get-relative-base () (relative-base) relative-base)

(def-state-writer set-relative-base (value) (relative-base)
  (setf new-relative-base value))

(def-state-reader get-input-source () (input-source) input-source)

(def-state-writer set-input-source (value) (input-source)
  (setf new-input-source value))


(defun get-input ()
  (with-state-monad
    (assign input-source (get-input-source))
    (if input-source
	(state-unit (funcall input-source :get-input))
	(front-input))))

(defun provide-input (value)
  (push-back-input (if (stringp value) 
                       (map 'list #'char-code value)
                       value)))

(defun input-available ()
  (with-state-monad
    (assign input-source (get-input-source))
    (if input-source
        (state-unit (funcall input-source :input-available))
        (with-state-monad
          (assign input-size (queue-length 'input))
          (state-unit (> input-size 0))))))

(defun get-output (&key (ascii nil))
  (with-state-monad
    (assign output (front-output))
    (state-unit (if ascii (code-char output) output))))


(defun get-all-output (&key (ascii nil))
  (labels ((get-all-output-rec (ascii)
             (with-state-monad
               (assign first (get-output :ascii ascii))
               (assign more (output-available))
               (if more
	           (with-state-monad
	             (assign rest (get-all-output-rec ascii))
	             (state-unit (cons first rest)))
	           (state-unit (list first))))))
    (with-state-monad
      (assign output-available (output-available))
      (if output-available
          (with-state-monad
            (assign output (get-all-output-rec ascii))
            (state-unit (if ascii
                            (format nil "狺秕麴豸秕麴豸┅篝狒瀛躅轸铋飑┅┅ㄤ彐躅痱秭殇瀛秕麴豸鲠祯濠瘐箬忉汶秕麴豸鲠祯濠ㄤ彐躅秕麴豸狯衢灬忪ī鏖翳篝狒瀛盹钺ㄡ篌殓秕麴豸箝聃艴瀛戾铉翳э豸瘐舂篝狒瀛躅轸秕麴豸箝癌┅ㄤ彐躅珏舡镳泔溴ī鏖翳篝狒瀛盹钺ㄡ篌殓沲颦轲ㄧ弭轲┅ㄡ篌殓沲颦轭篝蝓泗轱ㄧ弭礤盹蝙沲颦轲┅篝狒瀛躅轸ㄥ汜箦ㄤ邈镤瀛镳泔溴沲颦轭篝蝓泗轱瞟ū横滗ú喉蹯糸痨ǔ洪铕豸ù猴豸瘐舂ǖ宏蹴瓠殒趄蹂ǘ宏蹴瓠殒驷祗濠ǚ红弩蟓翳犷ǜ哄聃犰螬ü横潢躞舡蝈灬糸鲥忉箦ü鸿犰舂┅┅ㄤ彐躅溴泔溴疳蜥礤翦颦盹溴疳蜥礤翦轭篝蝓泗轱瞟戾è鲠盹ㄦ祜矧轭篝蝓泗轱ㄥ汜箦疳蜥礤翦ū卑癌ú卑鞍ǔ卑鞍癌┅卑┅ㄥ汜箦鲠ò吼矬轸轱瞟ū洪眄邃獒翦ú候屐狒轹濠┅ㄤ彐躅溴泔溴镳泔溴ㄩ铙趄蹉糸镱盹轭篝蝓泗轱卑癌ㄤ彐躅珏舡疳蜥礤翦瞟鏖翳篝狒瀛盹钺ㄡ篌殓沲颦轲ㄧ弭轲┅ㄡ篌殓沲颦轭篝蝓泗轱ㄧ弭礤盹蝙沲颦轲┅ㄡ篌殓疳蜥礤翦ㄧ弭礤盹蝙ǐ沲颦轲瞟┅ㄥ汜箦ㄤ邈镤瀛疳蜥礤翦颦盹溴沲颦轭篝蝓泗轱瞟ê痫箝糸镱ㄧ弭礤盹蝙疳蜥礤翦颟ê轫礤溟狒篝狒瀛躅轸疳蜥礤翦颟ê蝈灬糸鲥鏖翳篝狒瀛盹钺ㄡ篌殓蝈灬糸鲥忉箦ㄧ弭蝈灬糸鲥忉箦┅ㄧ弭礤盹蝙ǐ疳蜥礤翦蝈灬糸鲥忉箦┅┅┅ㄤ彐躅箦舡疳蜥礤翦鲠祯濠鏖翳篝狒瀛盹钺ㄡ篌殓沲颦轲ㄧ弭轲┅ㄡ篌殓沲颦轭篝蝓泗轱ㄧ弭礤盹蝙沲颦轲┅ㄡ篌殓疳蜥礤翦ㄧ弭礤盹蝙ǐ沲颦轲瞟┅ㄥ汜箦ㄤ邈镤瀛疳蜥礤翦颦盹溴沲颦轭篝蝓泗轱瞟ê痫箝糸镱箦舡礤盹蝙疳蜥礤翦鲠祯濠ê蝈灬糸鲥鏖翳篝狒瀛盹钺ㄡ篌殓蝈灬糸鲥忉箦ㄧ弭蝈灬糸鲥忉箦┅箦舡礤盹蝙ǐ疳蜥礤翦蝈灬糸鲥忉箦鲠祯濠┅┅ㄤ彐躅徜鲠钽瀛轲瞟鏖翳篝狒瀛盹钺ㄡ篌殓沲颦轲ㄧ弭轲┅箦舡轲ǐ沲颦轲瞟┅ㄤ彐躅忾钺蝙镳弪狒轱镳鏖翳篝狒瀛盹钺ㄡ篌殓ㄧ弭疳蜥礤翦暴ㄡ篌殓ㄧ弭疳蜥礤翦博箦舡疳蜥礤翦ㄦ躅汜祆镳猢┅ㄤ彐躅徜īㄢ轭狎镳弪狒轱＇┅ㄤ彐躅眭祠轲禊īㄢ轭狎镳弪狒轱＇┅ㄤ彐躅篝屦痱镧蜥ī鏖翳篝狒瀛盹钺ㄡ篌殓镳泔溴ㄧ弭镳泔溴┅ㄥ汜箦镳泔溴è横滗喉蹯糸痨鏖翳篝狒瀛盹钺ㄥ汜箦镳泔溴ê徜ㄡ滗┅ê眭祠轲禊眭祠轲禊┅ㄡ漩犷沐轲穿篝狒瀛躅轸舂┅ê轭瘐鏖翳篝狒瀛盹钺ㄡ篌殓鲠祯ㄧ弭轭瘐舂箦舡疳蜥礤翦鲠祯濠ㄡ漩犷沐轲博篝狒瀛躅轸舂┅ê秕麴豸鏖翳篝狒瀛盹钺ㄡ篌殓鲠祯ㄧ弭疳蜥礤翦暴痱秭殇瀛秕麴豸鲠祯濠ㄡ漩犷沐轲博篝狒瀛躅轸舂┅è宏蹴瓠殒趄蹂宏蹴瓠殒驷祗濠鏖翳篝狒瀛盹钺ㄡ篌殓翦篝ㄧ弭疳蜥礤翦暴ㄡ篌殓翎蜱弭ㄧ弭疳蜥礤翦博ㄩㄥ汜箦镳泔溴ê牾眇殒驷祗翦篝┅ê牾眇殒趄蹂ǒ翦篝┅箦舡轲翎蜱弭ㄡ漩犷沐轲畅篝狒瀛躅轸舂┅è红弩蟓翳犷哄聃犰螬鏖翳篝狒瀛盹钺ㄡ篌殓疳蜥肀ㄧ弭疳蜥礤翦暴ㄡ篌殓疳蜥聿ㄧ弭疳蜥礤翦博箦舡疳蜥礤翦ㄩㄥ汜箦镳泔溴ê戾篌翳犷疳蜥肀疳蜥聿┅ê羼踽祗疳蜥肀疳蜥聿┅癌ㄡ漩犷沐轲穿篝狒瀛躅轸舂┅ê徜牾篝蝈灬糸鲥忉箦鏖翳篝狒瀛盹钺ㄡ篌殓徜牾篝怡ㄧ弭疳蜥礤翦暴ㄡ篌殓沲颦蝈灬糸鲥忉箦ㄧ弭蝈灬糸鲥忉箦┅箦舡蝈灬糸鲥忉箦ǐ沲颦蝈灬糸鲥忉箦徜牾篝怡┅ㄡ漩犷沐轲博篝狒瀛躅轸舂┅ê栳祠鏖翳篝狒瀛盹钺篝狒瀛躅轸铋飑┅┅ㄤ彐躅蝓瞽躅糸飙栳祠ī鏖翳篝狒瀛盹钺ㄡ篌殓泔铘轭蹂篝屦痱镧蜥愆ㄩ泔铘轭蹂蝓瞽躅糸飙栳祠篝狒瀛躅轸铋飑┅ㄤ彐躅蝓瞽躅糸飙秕麴豸矧栳祠ī鏖翳篝狒瀛盹钺ㄡ篌殓泔铘轭蹂篝屦痱镧蜥愆ㄩ泔铘轭蹂鏖翳篝狒瀛盹钺ㄡ篌殓秕麴豸狯衢灬忪秕麴豸狯衢灬忪濠ㄩ秕麴豸狯衢灬忪篝狒瀛躅轸舂蝓瞽躅糸飙秕麴豸矧栳祠┅篝狒瀛躅轸铋飑┅ㄤ彐躅蝓瞽躅糸飙轭瘐舡矧栳祠ī鏖翳篝狒瀛盹钺ㄡ篌殓镳泔溴ㄧ弭镳泔溴┅ㄡ篌殓轭瘐舡狯衢灬忪ㄩ铕豸狯衢灬忪濠ㄩㄡ钿ㄥ镳泔溴洪铕豸铒轭瘐舡狯衢灬忪濠篝狒瀛躅轸舂鏖翳篝狒瀛盹钺ㄡ篌殓泔铘轭蹂篝屦痱镧蜥愆ㄩ泔铘轭蹂蝓瞽躅糸飙轭瘐舡矧栳祠篝狒瀛躅轸铋飑┅┅